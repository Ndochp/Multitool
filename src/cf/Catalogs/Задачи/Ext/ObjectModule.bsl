#Область ОбработчикиСобытий //EventHandlers
Процедура ПередЗаписью(Отказ)
	
	ЧтоМенять = Новый Структура;
	КонтролируемыеРеквизитыСтрокой = СтрСоединить(РегистрыСведений.КэшированныеРеквизитыРодителейЗадач.КонтролируемыеРеквизиты(), ", ");
	Если Не ЭтоНовый() Тогда 
		// Если меняется не родитель, то только по своим данным заполняем детей
		ПрошлыеЗначения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, КонтролируемыеРеквизитыСтрокой, Истина);
		Для Каждого КЗ из ПрошлыеЗначения Цикл
			Если КЗ.Значение <> ЭтотОбъект[Кз.Ключ] Тогда
				ЧтоМенять.Вставить(КЗ.Ключ, ЭтотОбъект[Кз.Ключ]);
			КонецЕсли;
		КонецЦикла;
		// если меняется родитель - все, звиздец полный пересчет
		Если ЧтоМенять.Свойство("Родитель") Тогда
			ЧтоМенять = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Родитель, КонтролируемыеРеквизитыСтрокой, Истина);
			ЧтоМенять.Удалить("Родитель");
			ОбновитьКэш(ЧтоМенять, Ссылка);
			// здесь мы не восстанавливаем разницу с начальным списком, а уточняем разницу с текущим родителем
			ЧтоМенять = УточнитьЧтоМенять(ЧтоМенять, ЭтотОбъект); 
		КонецЕсли;
	КонецЕсли;
	Если ЭтоНовый() и ЗначениеЗаполнено(Родитель) Тогда
		// Просто обновляем кэш для этой позиции
		НоваяСслка = Справочники.Задачи.ПолучитьСсылку(Новый УникальныйИдентификатор);
		ЭтотОбъект.УстановитьСсылкуНового(НоваяСслка);
		ЧтоМенять = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Родитель, КонтролируемыеРеквизитыСтрокой, Истина);
		ЧтоМенять.Удалить("Родитель");
		ОбновитьКэш(ЧтоМенять, НоваяСслка);
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЧтоМенять", ЧтоМенять);
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ДополнительныеСвойства.ЧтоМенять.Количество() <> 0 Тогда
		ОбновитьДетей(ДополнительныеСвойства.ЧтоМенять, Ссылка);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти //ОбработчикиСобытий //EventHandlers

#Область СлужебныеПроцедурыИФункции //Private
Процедура ОбновитьДетей(вхЧтоМенять, СсылкаРодителя)
	Перем Запрос;
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Родитель", СсылкаРодителя);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задачи.Ссылка КАК Ссылка,
	|	1 КАК ПоследняяКолонка
	|ИЗ
	|	Справочник.Задачи КАК Задачи
	|ГДЕ
	|	Задачи.Родитель = &Родитель"
	;
	
	// Дополнить данными из что менять
	ТекстДопКолонок = Новый Массив;
	Для Каждого КЗ из вхЧтоМенять Цикл
		СтрокаКолонки = стрШаблон("Задачи.%1 как %1", КЗ.Ключ);
		ТекстДопКолонок.Добавить(СтрокаКолонки);
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "1 КАК ПоследняяКолонка", СтрСоединить(ТекстДопКолонок, ", "));
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбработатьСсылкуИПотомков(вхЧтоМенять, Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьСсылкуИПотомков(Знач вхЧтоМенять, Знач ДанныеОбъекта)
	
	Перем ЧтоМенять;
	
	// 1. Обновить кэш
	ОбновитьКэш(вхЧтоМенять, ДанныеОбъекта.Ссылка);
	// 2. Обновить внуков если надо
	ЧтоМенять = УточнитьЧтоМенять(вхЧтоМенять, ДанныеОбъекта);
	
	Если ЧтоМенять.Количество() <> 0 Тогда 
		ОбновитьДетей(ЧтоМенять, ДанныеОбъекта.Ссылка);
	КонецЕсли;

КонецПроцедуры

Функция УточнитьЧтоМенять(Знач вхЧтоМенять, Знач ДанныеОбъекта)
	
	Перем КЗ, ЧтоМенять;
	
	ЧтоМенять = Новый Структура;
	Для Каждого КЗ из вхЧтоМенять Цикл
		Если Не ЗначениеЗаполнено(ДанныеОбъекта[КЗ.Ключ]) Тогда
			ЧтоМенять.Вставить(КЗ.Ключ, КЗ.Значение);
		КонецЕсли;
	КонецЦикла;
	Возврат ЧтоМенять;

КонецФункции

Процедура ОбновитьКэш(Знач вхЧтоМенять, Знач Ссылка)
	
	Перем МенеджерЗаписи;
	
	МенеджерЗаписи = РегистрыСведений.КэшированныеРеквизитыРодителейЗадач.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Задача = Ссылка;
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() Тогда // сбросилось
		МенеджерЗаписи.Задача = Ссылка;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, вхЧтоМенять);
	МенеджерЗаписи.Записать();

КонецПроцедуры
#КонецОбласти //СлужебныеПроцедурыИФункции //Private


